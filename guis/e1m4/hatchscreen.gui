windowDef Desktop
{
	rect	0 ,0 ,640 ,480
	background	"guis/assets/cpuserver/bg.tga"
	matcolor	1, 0.9, 1, 1
	visible	1

	windowDef StartLine
	{
		rect 40, 40, 560, 400		
		forecolor	0.2, 0.7, 0.2, 1.0			
		text ">:"
		textscale .8
		font "fonts/english"
		
		windowDef InputLine
		{
			rect 35, 15, 500, 300
			forecolor	0.2, 0.7, 0.2, 1.0			
			text "4 8 15 16 23 42"
			textscale .5
			font "fonts/english"
		}
		
		windowDef Cursor
		{
			rect 235, 15, 15, 30
			background	"guis/assets/e1m3/mask.tga"
			matcolor	0.2, 0.7, 0.2, 1.0		
			
			onTime 0
			{
				set "Cursor::matcolor" "0.2 0.7 0.2 1.0" ;
			}
			
			onTime 500
			{
				transition "Cursor::matcolor" "0.2 0.7 0.2 1.0" "0.2 0.7 0.2 0.0" "500" ;
			}
			
			onTime 1000
			{				
				resetTime "Cursor" 0
			}
		}
	}

	windowDef ButtonOverlay
	{
		rect	80, 60, 480, 360				
		matcolor	0.6, .7, 0.5, 0.4
		textscale 0.5
		forecolor	0.6, 7, 0.5, 0.2		
		visible "gui::gui_parm14"
				
		onMouseEnter
		{
			if( "gui::gui_parm13" == 0 )
			{
				transition "ButtonOverlay::matcolor" "0.6 .7 0.5 0.2" "0.4 .7 0.2 .4" "200" ;
				transition "ButtonOverlay::forecolor" "0.6 .7 0.5 0.2" "0.4 .7 0.2 .4" "200" ;				
				transition "Textangle::matcolor" "0.4 .6 0.4 0.2" "0.4 .6 0.4 .4" "200" ;
				transition "Textangle::forecolor" "0.8 1 0.7 0.2" "0.8 1 0.7 .8" "200" ;
			}
		}
		
		onMouseExit
		{
			if( "gui::gui_parm13" == 0 )
			{
				transition "ButtonOverlay::matcolor" "0.4 .7 0.2 .8" "0.6 .7 0.5 0.2" "200" ;
				transition "ButtonOverlay::forecolor" "0.4 .7 0.2 .8" "0.6 .7 0.5 0.2" "200" ;				
				transition "Textangle::matcolor" "0.4 .6 0.4 .4" "0.4 .6 0.4 0.2" "200" ;
				transition "Textangle::forecolor" "0.8 1 0.7 .8" "0.8 1 0.7 0.2" "200" ;
			}
		}
		
		onAction
		{
			if( "gui::gui_parm13" == 0 )
			{
				runScript "gui::gui_parm11" ;
			}			
		}

		windowDef Textangle
		{
			rect	60, 140, 350, 100	
			background "guis/assets/e1m3/backstuff.tga"
			matcolor 0.4, 0.6, 0.4, 0.2
			textscale 0.75
			forecolor	0.8, 1, 0.7, 0.4
			textalign 1
			textaligny 12
			text "gui::gui_parm1"
			visible 1
			font "fonts/bank"
		}
	}
}