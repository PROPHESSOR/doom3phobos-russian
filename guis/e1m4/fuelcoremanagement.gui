windowDef Desktop
{
	rect	0 ,0 ,640 ,480
	background	"guis/assets/cpuserver/bg.tga"
	matcolor	1, 0.9, 1, 1
	visible	1

	float "tankFilled" 0

	onTrigger
	{
		resetTime "Desktop" "0"
	}	
	
	onTime 0
	{
		if( "Desktop::tankFilled" == 0 )
		{
			//Initialize

			if( "gui::gui_parm1" == 1 )
			{				
				transition "FuelBox2Liquid::rect" "420 120 210 80" "420 120 0 80" "100";
				transition "FuelBox3Liquid::rect" "20 330 210 80" "230 330 0 80" "100";
			
				set "Fuel1DrainButtonOverlay::visible" "0";
				set "Fuel2DrainButtonOverlay::visible" "1";
				set "Fuel3DrainButtonOverlay::visible" "1";

				set "Fuel1FillText::visible" "0";
				set "Fuel2FillText::visible" "1";
				set "Fuel3FillText::visible" "1";
			}
			else if( "gui::gui_parm1" == 2 )
			{
				transition "FuelBox1Liquid::rect" "20 120 210 80" "230 120 0 80" "100";					
				transition "FuelBox3Liquid::rect" "20 330 210 80" "230 330 0 80" "100";
			
				set "Fuel1DrainButtonOverlay::visible" "1";
				set "Fuel2DrainButtonOverlay::visible" "0";
				set "Fuel3DrainButtonOverlay::visible" "1";

				set "Fuel1FillText::visible" "1";
				set "Fuel2FillText::visible" "0";
				set "Fuel3FillText::visible" "1";
			}
			else if( "gui::gui_parm1" == 3 )
			{
				transition "FuelBox1Liquid::rect" "20 120 210 80" "230 120 0 80" "100";
				transition "FuelBox2Liquid::rect" "420 120 210 80" "420 120 0 80" "100";
			
				set "Fuel1DrainButtonOverlay::visible" "1";
				set "Fuel2DrainButtonOverlay::visible" "1";
				set "Fuel3DrainButtonOverlay::visible" "0";

				set "Fuel1FillText::visible" "1";
				set "Fuel2FillText::visible" "1";
				set "Fuel3FillText::visible" "0";
			}
		}

		if( "Desktop::tankFilled" != "gui::gui_parm1" )
		{
			if( "Desktop::tankFilled" == 1 )
			{
				transition "FuelBox1Liquid::rect" "20 120 210 80" "230 120 0 80" "2000";	
				set "Fuel1DrainButtonOverlay::visible" "0";
			}
			else if( "Desktop::tankFilled" == 2 )
			{
				transition "FuelBox2Liquid::rect" "420 120 210 80" "420 120 0 80" "2000";
				set "Fuel2DrainButtonOverlay::visible" "0";
			}
			else if( "Desktop::tankFilled" == 3 )
			{
				transition "FuelBox3Liquid::rect" "20 330 210 80" "230 330 0 80" "2000";
				set "Fuel3DrainButtonOverlay::visible" "0";
			}

			set "Fuel1DrainButtonOverlay::visible" "0";
			set "Fuel2DrainButtonOverlay::visible" "0";
			set "Fuel3DrainButtonOverlay::visible" "0";

			if( "gui::gui_parm1" == 1 )
			{
				transition "FuelBox1Liquid::rect" "230 120 0 80" "20 120 210 80" "2000";		
								
				set "Fuel1FillText::visible" "0";
			}
			else if( "gui::gui_parm1" == 2 )
			{
				transition "FuelBox2Liquid::rect" "420 120 0 80" "420 120 210 80" "2000";	
				
				
				set "Fuel2FillText::visible" "0";
			}
			else if( "gui::gui_parm1" == 3 )
			{
				transition "FuelBox3Liquid::rect" "230 330 0 80" "20 330 210 80" "2000";
								
				set "Fuel3FillText::visible" "0";
			}
		}
	}

	onTime 2000
	{
		if( "Desktop::tankFilled" != "gui::gui_parm1" )
		{
			if( "gui::gui_parm1" == 1 )
			{				
				set "Fuel2DrainButtonOverlay::visible" "1";
				set "Fuel2FillText::visible" "1";
				set "Fuel3DrainButtonOverlay::visible" "1";
				set "Fuel3FillText::visible" "1";
			}
			else if( "gui::gui_parm1" == 2 )
			{				
				set "Fuel1DrainButtonOverlay::visible" "1";
				set "Fuel1FillText::visible" "1";
				set "Fuel3DrainButtonOverlay::visible" "1";
				set "Fuel3FillText::visible" "1";
			}
			else if( "gui::gui_parm1" == 3 )
			{			
				set "Fuel1DrainButtonOverlay::visible" "1";
				set "Fuel1FillText::visible" "1";
				set "Fuel2DrainButtonOverlay::visible" "1";
				set "Fuel2FillText::visible" "1";
			}
			set "Desktop::tankFilled" "gui::gui_parm1" ;
		}		
	}
	
	windowDef FuelBox1
	{
		rect	100, 75, 110, 300
		background	"guis/assets/e1m3/mask.tga"
		matcolor	0, 0, 0, 0.6
		visible	1		
	}

	windowDef FuelBoxCoreLiquid
	{
		rect	240, 60, 160, 360
		background	"guis/assets/e1m3/mask.tga"
		matcolor	0.1, .2, 0.5, 0.7
		visible 1
	}

	windowDef FuelBox1Liquid
	{
		rect	20, 120, 210, 80
		background	"guis/assets/e1m3/mask.tga"
		matcolor	0.1, .2, 0.5, 0.7
		visible 1
	}

	windowDef FuelBox2Liquid
	{
		rect	420, 120, 210, 80
		background	"guis/assets/e1m3/mask.tga"
		matcolor	0.1, .2, 0.5, 0.7
		visible 1
	}

	windowDef FuelBox3Liquid
	{
		rect	20, 330, 210, 80
		background	"guis/assets/e1m3/mask.tga"
		matcolor	0.1, .2, 0.5, 0.7
		visible 1
	}
	
	windowDef FuelBox4Liquid
	{
		rect	420, 330, 210, 80
		background	"guis/assets/e1m3/mask.tga"
		matcolor	0.6, 0.2, 0.1, pdhalffade[ time * 0.002 ] + 0.3
		visible 1	
	}	
	
	windowDef FuelBox1Fill
	{
		rect	20, 120, 210, 80
		background	"guis/assets/e1m3/mask.tga"
		matcolor	.3, 0.6, 0.15, 0
		visible 1
	}

	windowDef FuelBox2Fill
	{
		rect	420, 120, 210, 80
		background	"guis/assets/e1m3/mask.tga"
		matcolor	.3, 0.6, 0.15, 0
		visible 1
	}

	windowDef FuelBox3Fill
	{
		rect	20, 330, 210, 80
		background	"guis/assets/e1m3/mask.tga"
		matcolor	.3, 0.6, 0.15, 0
		visible 1
	}

	windowDef Fuel1FillText
	{
		rect	90, 127, 200, 80
		text "оепемеярх"
		textscale	.3
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]
		rotate 21
		visible 1
	}	

	windowDef Fuel2FillText
	{
		rect	450, 165, 200, 80
		text "оепемеярх"
		textscale	.3
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]
		rotate -21
	}

	windowDef Fuel3FillText
	{
		rect	90, 332, 200, 80
		text "оепемеярх"
		textscale	.3
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]
		rotate 21
	}		
	
	windowDef Fuel4FailureText
	{
		rect	450, 370, 200, 80
		text "яани"
		textscale	.3
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]
		rotate -21
	}
				

	windowDef Bglow
	{
		rect	0, 0, 640, 480
		background	"guis/assets/cpuserver/bglow.tga"
		visible	1
		matcolor	0.6, 0.4, 0.2, 0.1
	}
	
	
	windowDef Transferring
	{
		rect	10 ,10 ,620 ,460
		background	"guis/assets/e1m3/backstuff"
		
		matcolor	1 ,0.6 ,0.3 , 0.3
		visible	0
		
		windowDef TransferText
		{
			rect 25, 200, 600, 100
			textscale	0.86
			forecolor	1, 0.7, 0.3, 0.5
		background	"gui/line/horizwarn2"				
			visible	0
			font	"fonts/micro"		
			text	"оепемня рнокхбю"
		}
	}

		windowDef OverlayLines
	{
		rect	5, 5, 630, 470		
		background	"guis/assets/e1m4/fuelcoreback.tga"
		matcolor	1, 0.85, 0.4, 1.0
		visible	1
		
	}

		windowDef Text1
	{
		rect	230, 30, 540, 70
		text	"рнокхбмне ъдпн"
		textscale	0.50
		forecolor	1, 0.7, 0.3, 0.8
		visible	1
		font	"fonts/micro"
	}			
	

		windowDef FuelCoreText
	{
		rect 25, 80, 120, 60		
		text "рпсаю"
		textscale	.5
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]		
	}

	windowDef FuelCoreText2
	{
		rect 565, 80, 120, 60		
		text "рпсаю"
		textscale	.5
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]		
	}

	windowDef FuelCoreText3
	{
		rect 25, 285, 120, 60		
		text "рпсаю"
		textscale	.5
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]		
	}

	windowDef FuelCoreText4
	{
		rect 565, 285, 120, 60		
		text "рпсаю"
		textscale	.5
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]		
	}

	windowDef Fuel2Text
	{
		rect 565, 95, 120, 60		
		text "02"
		textscale	.8
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]		
	}

	
	windowDef Fuel1Text
	{
		rect 25, 95, 40, 40
		text "01"
		textscale	.8
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]		
	}

	windowDef Fuel3Text
	{
		rect 25, 300, 60, 40
		text "03"
		textscale	.8
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]		
	}
	
		
	windowDef Fuel4Text
	{
		rect 565, 300, 60, 40
		text "04"
		textscale	.8
		forecolor	1, 0.85, 0.4, pdflick [ time * .025 ]		
	}

	
	windowDef Static
	{
		rect	-10 ,-10 ,660 ,500
		visible	1
		background	"gui/static"
		matcolor	1, 1, 1, pdhalffade[ time * 0.001 ] / 10
	}
	windowDef Outerglow
	{
		rect	0 ,0 ,-640 ,480
		background	"guis/assets/common/outerglow"
		matcolor	1 ,0.8 ,0.3 , 0.2
		visible	1
	}
	windowDef Outershadow
	{
		rect	0 ,0 ,-640 ,480
		background	"guis/assets/common/outershadow"
		matcolor	1 ,1 ,1 ,1
		visible	1
	}
	windowDef Addhighlight
	{
		rect	0 ,0 ,-640 ,-480
		background	"gui/addhighlight"
		matcolor	0.5 ,0.3 ,0.1 ,1
		visible	1
	}
	windowDef scanlinesAlt
	{
		rect	0,0,640,480
		matcolor	0.1,0.1,0.1,1
		background	"gui/test/gui_scanlines5"
		matscaley	2
	}
	windowDef OuterGlowBar
	{
		rect	0 ,0 ,-640 ,480
		background	"gui/outerglowbar"
		matcolor	1 ,0.7 ,0.4 ,1
		visible	0
	}
	windowDef Reflect
	{
		rect	10 ,-10 ,-620 ,470
		matcolor	1 ,0.8 ,0.5 ,0.1
		visible	0
		background	"gui/reflect2"
	}
	windowDef Dirt2
	{
		rect	0,0,-640,480
		matcolor	1, 1, 1 ,0.7
		visible	1
		background	"guis/assets/common/dirt2"
		noevents	1
	}
	windowDef Dirt21
	{
		rect	0,0,-640,480
		matcolor	1, 1, 1 ,0.5
		visible	1
		background	"guis/assets/common/dirt4"
		noevents	1
	}
	windowDef GlassScratch1
	{
		rect	-36,-7,839,-530
		background	"guis/assets/common/scratch1d"
		visible	1
		matcolor	0,0,0,0.4
	}
	windowDef GlassScratch
	{
		rect	-36,-10,839,-530
		background	"gui/common/scratch1d"
		visible	1
		matcolor	0.6,0.6,0.6,1
	}
	windowDef Dirt211
	{
		rect	0,0,-640,-480
		matcolor	1, 1, 1 ,0.5
		visible	1
		background	"guis/assets/common/dirt4"
		noevents 1	
	}	
	
			windowDef Addhighlight1
	{
		rect	0,0,-640,480
		background	"gui/addhighlight"
		matcolor	0.6,0.6,0.4,1
		visible	1
	}
	windowDef scanlinesAlt11
	{
		rect	0,0,-640,480
		matcolor	0.15,0.15,0.15,1
		background	"gui/test/gui_scanlines5"
		matscaley	1.5
		visible	1
	}
	windowDef Static
	{
		rect	-9,-9,-660,500
		visible	1
		background	"gui/static"
		matcolor	1, 1, 1, pdhalffade[ time * 0.001 ] / 8
	}
	windowDef Outershadow
	{
		rect	0,0,-640,480
		background	"guis/assets/common/outershadow"
		matcolor	1 ,1 ,1 ,1
		visible	1
	}
	windowDef Dirt
	{
		rect	0,0,-640,480
		matcolor	1, 1, 1 ,0.2
		visible	1
		background	"guis/assets/common/dirt3"
		noevents	1
	}
	windowDef Reflect
	{
		rect	10,5,-620,470
		matcolor	1 ,1 ,1 ,0.2
		visible	0
		background	"gui/reflect1"
	}
	windowDef bgScroll
	{
		rect	20,15,-600,450
		visible	1
		background	"gui/scrollbig"
		matcolor	1, 1,  1, flickertable [ time * 5 ] / 20
	}
	
	//20, 420 - 120, 330
	windowDef Fuel1DrainButtonOverlay
	{
		rect	20, 120, 210, 80
		visible 1
				
		onMouseEnter
		{
		
			transition "FuelBox1Fill::matcolor" ".3 0.6 0.15 0" ".3 0.6 0.15 1" "200" ;
		}
		
		onMouseExit
		{
			transition "FuelBox1Fill::matcolor" ".3 0.6 0.15 1" ".3 0.6 0.15 0" "200" ;
		}
		
		onAction
		{
			runScript "gui::gui_parm11" ;
		}
	}	

	windowDef Fuel2DrainButtonOverlay
	{
		rect	420, 120, 210, 80
		visible 1
				
		onMouseEnter
		{
		
			transition "FuelBox2Fill::matcolor" ".3 0.6 0.15 0" ".3 0.6 0.15 1" "200" ;
		}
		
		onMouseExit
		{
			transition "FuelBox2Fill::matcolor" ".3 0.6 0.15 1" ".3 0.6 0.15 0" "200" ;
		}
		
		onAction
		{
			runScript "gui::gui_parm12" ;
		}
	}	

	windowDef Fuel3DrainButtonOverlay
	{
		rect	20, 330, 210, 80
		visible 1
				
		onMouseEnter
		{
		
			transition "FuelBox3Fill::matcolor" ".3 0.6 0.15 0" ".3 0.6 0.15 1" "200" ;
		}
		
		onMouseExit
		{
			transition "FuelBox3Fill::matcolor" ".3 0.6 0.15 1" ".3 0.6 0.15 0" "200" ;
		}
		
		onAction
		{
			runScript "gui::gui_parm13" ;
		}
	}	
	

}