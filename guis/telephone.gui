windowDef Desktop
{
	rect	128,0,384,480
	backcolor 0, 0, 0, 1

	onTrigger
	{
		if("gui::gui_parm1" == 0)
		{
			set "LockScreen::visible" 1;
			set "PhoneCallScreen::visible" 0;
		}
		else if("gui::gui_parm1" == 1)
		{
			set "BackCircle::rect" "40 40 96 72";
			set "BackCircle::matcolor" "0 .125 0 1";
			set "Text::forecolor" "1 1 1 1";		
			set "Text::rect" "140 40 320 48";
			set "DeclineText::forecolor" "1 1 1 1";
			set "IncomingCallText::rect" "0 230 384 100";
			set "IncomingCallText::forecolor" "1 1 1 1";
			set "Portrait::rect" "64 32 256 192";
			set "CallerText::rect" "0 0 384 100";
			set "CallerText::forecolor" "1 1 1 0";
			set "TelephoneAccept::rect" "0 0 384 128";
			set "TelephoneDecline::rect" "0 64 384 128";
			
			set "LockScreen::visible" 0;
			set "PhoneCallScreen::visible" 1;
		}
		else if("gui::gui_parm1" == 2)
		{
			resetTime "PhoneCallScreenButtons" "0";
		}
	}

	windowDef PhoneCallScreen
	{
		rect 0,0,384,480
		background "guis/assets/telephone_background.tga"
		visible 0

		windowDef Portrait
		{
			rect 64, 32, 256, 192
			background "guis/assets/border_hori"
			matcolor 1,1,1,1

			windowDef Picture
			{
				rect 8, 6, 240, 180
				background "guis/assets/lee_portrait.tga"
			}
		}

		windowDef IncomingCallText
		{
			rect 0,230, 384, 100
			textalign 1
			textscale .7
			text "«вонит Ћи"

			onTime 0
			{
				set text "«вонит Ћи"
			}

			onTime 500
			{
				set text "«вонит Ћи."
			}

			onTime 1000
			{
				set text "«вонит Ћи.."
			}

			onTime 1500
			{
				set text "«вонит Ћи..."
			}

			onTime 2000
			{
				resetTime 0;
			}
		}

		windowDef CallerText
		{
			rect 0,0, 384, 100

			textalign 1
			textaligny -8
			textscale .7
			text "Ћи"
			forecolor 1,1,1,0
		}

		windowDef PhoneCallScreenButtons
		{
			rect 0,280,384,480
			visible 1
						
			windowDef TelephoneAccept
			{
				rect 0,0,384,128
				noTime 0

				windowDef BackCircle
				{
					rect 40,40,96,72

					matcolor 0,.125,0,1
					background "guis/assets/circle.tga"				
				}

				windowDef Text
				{
					rect 140, 40, 320, 48
					text "ѕрин`ть вызов"
					textaligny 16
					textscale .6
				}

				windowDef Circle
				{
					rect 40,40,96,72

					matcolor 0,.125,0,1
					background "guis/assets/circle.tga"

					windowDef Icon
					{
						rect 16,12,64,48

						matcolor 0,.75,0,1
						background "guis/assets/telephone.tga"
					}
				}
			}
		
			windowDef TelephoneDecline
			{
				rect 0,64,384,128
				noTime 0

				windowDef DeclineText
				{
					rect 77, 40, 320, 48
					text "ќтклонить"
					textaligny 16
					textscale .6
				}

				windowDef DeclineCircle
				{
					rect 248,40,96,72

					matcolor .125,0,0,1
					background "guis/assets/circle.tga"

					windowDef DeclineIcon
					{
						rect 16,12,64,48

						matcolor .75,0,0,1
						background "guis/assets/telephone2.tga"
					}
				}
			}

			onTime 0
			{
				if("gui::gui_parm1" == 2)
				{
					transition "BackCircle::rect" "40 40 96 72" "8 16 160 120" "250";
					transition "BackCircle::matcolor" "0 .125 0 1" "0 .5 0 1" "250";
					transition "Text::forecolor" "1 1 1 1" "0 1 0 1" "250";
				}
			}

			onTime 300
			{
				if("gui::gui_parm1" == 2)
				{
					transition "BackCircle::matcolor" "0 .5 0 1" "0 1 0 1" "100";
					transition "Text::forecolor" "0 1 0 1" "1 1 1 1" "100";
				}
			}

			onTime 400
			{
				if("gui::gui_parm1" == 2)
				{
					transition "BackCircle::matcolor" "0 1 0 1" "0 0 0 0" "500";
					transition "Text::rect" "140 40 320 48" "240 40 320 48" "250";
					transition "Text::forecolor" "1 1 1 1" "1 1 1 0" "250";
					transition "DeclineText::forecolor" "1 1 1 1" "1 1 1 0" "500";

					transition "IncomingCallText::rect" "0 230 384 100" "0 250 384 100" "500";
					transition "IncomingCallText::forecolor" "1 1 1 1" "1 1 1 0" "500";
				
					transition "Portrait::rect" "64 32 256 192" "64 52 256 192" "500";
					transition "CallerText::rect" "0 0 384 100" "0 20 384 100" "500";
					transition "CallerText::forecolor" "1 1 1 0" "1 1 1 1" "500";
				}
			}

			onTime 900
			{
				if("gui::gui_parm1" == 2)
				{
					transition "TelephoneAccept::rect" "0 0 384 128" "40 32 384 128" "250";
					transition "TelephoneDecline::rect" "0 64 384 128" "-40 32 384 128" "250";
				}
			}
		}
	}

	windowDef LockScreen
	{
		rect 0,0,384,480
		visible 1

		windowDef LockScreenInfo
		{
			rect 112,210, 160, 200
			text "22:35"
			forecolor 1,1,1,1
			
			font "fonts/bank"
			textscale .6
			textalign 1

			windowDef BlackBar
			{
				rect 75,15,10,20
				visible 1
				backcolor 0,0,0,1

				onTime 0
				{
					set visible 1;
				}

				onTime 1000
				{
					set visible 0;
				}

				onTime 2000
				{
					resetTime 0;
				}
			}

			windowDef Date
			{
				rect 0,35,160,100
				text "¬—, 21 ќ “яЅ–я"
				textscale 0.2
				textalign 1
			}

			windowDef BatteryInfo
			{
				rect 0, 50, 160, 100				

				windowDef BatteryText
				{
					rect 0, 0, 80, 100
					text "89%"
					textscale 0.2
					textalign 2				
					forecolor 1,1,1,1
				}

				windowDef BatteryIcon
				{
					rect 90, 2, 4, 2
					backcolor 1,1,1,1
				}
			
				windowDef BatteryIcon2
				{
					rect 88, 4, 8, 8
					backcolor 1,1,1,1
				}
			}

			onTime 0
			{
				set rect "112 210 160 200"
			}

			onTime 9000
			{
				set rect "240 310 160 200"
			}

			onTime 18000
			{
				set rect "224 110 160 200"
			}

			onTime 27000
			{
				set rect "40 10 160 200"
			}

			onTime 36000
			{
				set rect "200 310 160 200"
			}


			onTime 45000
			{
				resetTime 0;
			}
		}
	}
}